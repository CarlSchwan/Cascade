/*
 *  Cascade Image Editor
 *
 *  Copyright (C) 2022 Till Dechent and contributors
 *
 *  This program is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with this program.  If not, see <https://www.gnu.org/licenses/>.
*/

#version 430

layout (local_size_x = 16, local_size_y = 16) in;
layout (binding = 0, rgba32f) uniform readonly image2D inputImage;
layout (binding = 1, rgba32f) uniform readonly image2D mask;
layout (binding = 2, rgba32f) uniform image2D resultImage;

layout(set = 0, binding = 3) uniform InputBuffer
{
    layout(offset = 0) float bRed;
    layout(offset = 4) float bGreen;
    layout(offset = 8) float bBlue;
    layout(offset = 12) float bAlpha;
    layout(offset = 16) float strength;
    layout(offset = 20) float hasMask;
    layout(offset = 24) float shaderPass;
} sb;

int width = imageSize(inputImage).x;
int height = imageSize(inputImage).y;

int strength = int(sb.strength);

int div = 2 * strength + 1;

void main()
{
    ivec2 pixelCoords = ivec2(gl_GlobalInvocationID.xy);

    vec4 pixel = imageLoad(inputImage, pixelCoords.xy).rgba;

    vec4 sum = pixel * abs(vec4(sb.bRed, sb.bGreen, sb.bBlue, sb.bAlpha) - 1.0);

    if (sb.strength != 0)
    {
        if (sb.shaderPass == 0.0)
        {
            for (int i = -strength; i <= strength; ++i)
            {
                if (pixelCoords.x + i < 0 || pixelCoords.x + i > width)
                {
                    sum += imageLoad(inputImage, ivec2(pixelCoords.x - i, pixelCoords.y)).rgba;
                }
                else
                {
                    sum += imageLoad(inputImage, ivec2(pixelCoords.x + i, pixelCoords.y)).rgba;
                }
            }
        }
        else
        {
            for (int i = -strength; i <= strength; ++i)
            {
                if (pixelCoords.y + i < 0 || pixelCoords.y + i > height)
                {
                    sum += imageLoad(inputImage, ivec2(pixelCoords.x, pixelCoords.y - i)).rgba;
                }
                else
                {
                    sum += imageLoad(inputImage, ivec2(pixelCoords.x, pixelCoords.y + i)).rgba;
                }
            }
        }

        sum = sum / div;

        if (sb.bRed == 0.0)
        {
            sum.r = pixel.r;
        }
        if (sb.bGreen == 0.0)
        {
            sum.g = pixel.g;
        }
        if (sb.bBlue == 0.0)
        {
            sum.b = pixel.b;
        }
        if (sb.bAlpha == 0.0)
        {
            sum.a = pixel.a;
        }
        imageStore(resultImage, ivec2(gl_GlobalInvocationID.xy), sum);
    }
    else
    {
        imageStore(resultImage, ivec2(gl_GlobalInvocationID.xy), pixel);
    }


}
